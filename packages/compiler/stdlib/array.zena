export final extension class FixedArray<T> on array<T> {
  @intrinsic('array.len')
  declare length: i32;

  constructor(length: i32, value: T) {
    super(__array_new(length, value));
  }

  @intrinsic('array.get')
  declare operator [](index: i32): T;

  @intrinsic('array.set')
  declare operator []=(index: i32, value: T): void;

  map<U>(f: (item: T, index: i32, array: FixedArray<T>) => U): FixedArray<U> {
    let len = this.length;
    let result = __array_new_empty<U>(len);
    var i = 0;
    while (i < len) {
      result[i] = f(this[i], i, this);
      i = i + 1;
    }
    return result;
  }

  reverse(): FixedArray<T> {
    let len = this.length;
    let result = __array_new_empty<T>(len);
    var i = 0;
    while (i < len) {
      result[i] = this[len - 1 - i];
      i = i + 1;
    }
    return result;
  }
}

export class Array<T> {
  #buffer: FixedArray<T>;
  #length: i32 = 0;

  constructor(capacity: i32) {
    this.#buffer = __array_new_empty<T>(capacity);
  }

  length: i32 {
    get {
      return this.#length;
    }
  }

  push(value: T): void {
    if (this.#length == this.#buffer.length) {
      this._grow();
    }
    this.#buffer[this.#length] = value;
    this.#length = this.#length + 1;
  }

  pop(): T {
    this.#length = this.#length - 1;
    return this.#buffer[this.#length];
  }

  operator [](index: i32): T {
    return this.#buffer[index];
  }

  operator []=(index: i32, value: T): void {
    this.#buffer[index] = value;
  }

  _grow(): void {
    let newCapacity = this.#buffer.length * 2;
    let newBuffer = __array_new_empty<T>(newCapacity);
    var i = 0;
    while (i < this.#length) {
      newBuffer[i] = this.#buffer[i];
      i = i + 1;
    }
    this.#buffer = newBuffer;
  }
}
